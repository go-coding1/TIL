# 수업내용

### MVC

Model은 DAO,VO 라고 생각할 수 도 있지만 우리가 작성한 모든 비즈니스 로직을 말한다. 데이터를 가지고 비즈니스 적으로 처리해야하는 모든 부분이 Model이다. 짧게 설명하자면 **모델(Model)은 응용프로그램 객체이다.**

### BankApplication JDBC, PL/SQL 실습

JDBC 서브노트.txt에 마지막 부분 코드

### Queto SQL

문자열 붙일때 `ename || 'dj '' djkd '' '` 일케 하는데 Queto 문자로 붙이는 방법이 있다.

`q'! !'` 로 쓰고 저 사이에 원하는 문자열을 넣으면 된다. Tip!!

### PL/SQL 의 Exception

에러의 종류

* Logic Error

* Syntax Error

* Runtime Error(PL/SQL에서는 이것만 Exception이라고 부른다.)
  
  * Oracle-defined Exception(오라클이 정의한 예외 그중 극히 일부만 이름이 정해져 있다. 대부분 이름이 정해져 있지 않다.)
    
    Oracle-defined Exception은 `C:\app\LG\product\18.0.0\dbhomeXE\rdbms\admin`경로에 `stdspec.sql` 안에서 확인 할 수 있다.
  
  * User-defined Exception(사용자가 정의한 Exception)

PL/SQL의 Exception 처리시 이름 있을때 `when name then` 이름 없을땐 `when others then` 으로 처리한다. 

### Exception의 과정 5단계

1. when name then

2. put the name to exception then handling

3. when others then

4. declare, raise, handling

5. raise_application_error procedure

### Exception 처리하지 않을 경우 vs 처리한 경우

* 처리 안한 경우
  
  PL/SQL이 끝나면서 블록 안에서 exception을 찾는다. 없으면 위 블록을 찾고, 또 위의 블록을 찾는다.

* 처리 한 경우
  
  exception이 발생한 줄에서 exception 처리하는 내용을 실행후 블록을 빠져나간다.

* 만약 exception이 발생과 상관없이 무조건 다음 줄을 수행하고 싶으면?
  
  exception이 발생할 것 같은 곳을 하나의 블록으로 다시 감싸서 처리한다. exception이 발생해도 만들어준 블록만 빠져나가기 때문에 바깥 블록의 내용을 무조건 처리하게 된다.

### Exception Propagation

실행부에서 exception이 발생하면 블록을 빠져나간다. 근데 다른 곳에서 exception이 발생하면 어떻게 될까?? 선언부에서 exception이 발생하면??

선언부에서 발생한 exception은 곧바로 자신을 감싸고 있는 블록을 빠져나간다. 그럼 처리는??-> 블록으로 또 감싸주고 외부에 exception 처리 코드를 넣는다.

```plsql
DECLARE
    BEGIN
        V_NUMBER VARCHAR2(10);
        NULL
    END
BEGIN
   when other then
    ...
END
```

또는 예외처리를 하는 부분에서 exception이 발생해도 즉각적으로 블록을 빠져나간다.

**An exception raised in a declaration propagates immediately to the enclosing block.
 An exception raised inside a handler propagates immediately to the enclosing block.**

### Exception처리 5가지

1. 이름이 있는 exception
   
   ```plsql
   create or replace procedure p1(a number, b number)   
       is
       begin
         p.p(a/b);
       exception
         when zero_divide then
           p.p('0으로 나눌 수 없습니다!');
       end;
   ```

2. 이름이 없는 exception 처리
   
   `pragma` 키워드로 exception을 만들어줌
   
   단 만들어준 exception은 해당 프로시저에서만 쓸 수 있기 때문에 사용자 정의 패키지로 만들어서 사용할 수 있음
   
   근데 사용자정의.exception이름 쓰기 귀찮으니 `stdspec.sql` 에 Add new Exception here 부분에 정의 해 줘서 쉽게 사용할 수 있다.
   
   ```plsql
   create or replace package pack_exceptions
       is
         e_null exception;
         pragma exception_init(e_null, -1400);
       end;
   create or replace procedure insert_t1(a number)
    is
    begin
    insert into t1 values(a);
    exception
    when pack_exceptions.e_null then
    p.p('Null 값을 입력할 수 없습니다!');
    end;
   ```

3. when others then 처리
   
   ```plsql
   create or replace procedure insert_t1(a number)
    is
    v_error_code varchar2(60);
    v_error_message varchar2(60);
    begin
    insert into t1 values(a);
    exception
    when others then
    v_error_code := substr(sqlcode, 1, 30);
    v_error_message := substr(sqlerrm, 1, 30); 
   insert into t_errors
    values(sysdate, 'insert_t1', v_error_code, v_error_message );
    /*위 부분이 기니까 따로 에러를 넣는 insert_error_message 프로시저를 만들어도 된다.*/
    end;
   ```
   
   others는 어떤 에러인지 알 수 없기 때문에 `sqlcode sqlerm` 함수를 제공해 준다. 보통 이런 에러가 발생 했을 때는 에러 테이블을 따로 만들어서 예외처리할대 마다 insert를 해주는 작업을 해준다.

4. declare, raise, handling 처리
   
   원하는 부분에서 내가 만든 exception을 발생 시키고 싶을때 raise 키워드를 사용해서 블록을 빠져나가고 declare에서 만들어준 exception으로 이동되어 handling된다.
   
   ```plsql
       create or replace procedure p1(a number)
       is
         v_sal emp.sal%type;
         e_too_low exception;
       begin
         select sal into v_sal
         from emp
         where empno = a;
   
         if v_sal < 1000 then
           raise e_too_low;
         end if;
   
         p.p(a||' 사원의 급여는 '||v_sal||'입니다.');
   
       exception
         when e_too_low then
           p.p(a||' 사원의 급여가 최저 급여미만입니다. 확인해보세요!');
       end;
   ```

5. raise_application_error() 프로시저로 처리
   
   사용자 정의 에러를 Oracle에서 non-predefined 한 exception 처럼 사용할 수 있게 하는 프로시저이다.
   
   `RAISE_APPLICATION_ERROR (error_code, message[, {TRUE | FALSE}]);` 의 형태로 쓴다. error_code에 들어갈 수 있는 숫자의 범위는 -20000 ~ -209999 이다.
   
   ```plsql
       create or replace procedure p1(a number)
       is
         v_sal emp.sal%type;
         e_too_null exception;
         pragma exception_init(e_too_null, -20111);
       begin
         select sal into v_sal
         from emp
         where empno = a;
   
         if v_sal < 1000 then
           RAISE_APPLICATION_ERROR(-20111, a||' 사원의 급여가 최저 급여미만입니다. 확인해보세요!');
         end if;
   
         p.p(a||' 사원의 급여는 '||v_sal||'입니다.');
       exception
         when  e_too_null then
           p.p('예외 발생');
       end;
   ```

### 트리거(Trigger)를 이용함

특정 ip로 로그인하는 건 접근을 막고 싶을때 RAISE_APPLICATION_ERROR를 사용해서 막을 수 있다.

```plsql
CREATE OR REPLACE TRIGGER LOGON_TRI after LOGON ON ACE.SCHEMA
BEGIN
    if SUBSTR(sys_context('USERENV', 'IP_ADDRESS'), 1, 7) in ('127.0.0', '192.178')  then
        RAISE_APPLICATION_ERROR ( -20002, 'IP '||ORA_CLIENT_IP_ADDRESS
        || ' is not allowed to connect database as ACE!');
    END IF;
END;
```

### Subprogram Parameter(Formal)

Mode가 3가지가 있다

* IN(defualt)
  
  PL/SQL 안에서 새로운 값을 할당하지 못한다.

* OUT
  
  새로운 값을 할당해 줄 수 있다.
  
  호출시 받아줄 값(변수)를 지정해 줘야한다.

* IN OUT
  
  초기화된 변수 처럼 사용할 수 있다.

### (Actual) 매개 변수

* Positional
  
  ```plsql
  var g_sal number
  
  dbms_output.put_line(:g_sal);
  ```

* Named
  
  ```plsql
  val g_sal number
  
  dbms_output.put_line(a => :g_sal);
  ```

* Mixed Notation

### 치환변수, 바인드 변수, PL/SQL 변수

```sql
var g_sal number;
```

바인드 변수는 서버쪽의 PGA 안에 생성된다.

```plsql
create or replace procedure p1
(
    p_name varchar2(30);
)
```

PL/SQL 변수는 오라클 인스턴스 서버 안에 생성된다.

```sql
p1(&v_name);
```

치환 변수는 사용자의 클라이언트 서버쪽에 생겨서 입력 받을시 sql문이 값을 할당해서 서버로 보낸다.

```plsql
var g_ename varchar2(30)
var g_sal number
--위 두 변수는 세션이 종료되기 전까지 유지된다.

--&sv_empno는 입력 받는 동안만 변수가 존재한
--v_empno는 pl/sql 문을 실행하는 동안만 DB Server에 존재한
declare
    v_empno emp.emno%type :=  &sv_empno;
begin
    select ename, sal
      into :g_name, :g_sal
      from emp;
end;

print g_ename g_sal
```

### Package

패키지는 패키지 스펙만 존재할 수도 있다. 상수나 커서만 잔뜩 쌓아놓고 만들 수 있다. 패키지 body는 혼자만 존재할 수 없다.

패키지는 public 과 private로 접근지정자가 존재한다. 스펙에 정의한 변수나 프로시저, 함수는 public 이고 body에 정의한 변수나 프로시저, 함수는 private이다.

variable의 접근지정자는 public private local 3가지 이다.
SubProgram 입장에서는 public private local 3가지 이다.

```plsql
create or replace package pack1
  is 
    v1 number;                           -- public variable
    procedure setV2(a number);              -- public subprogram
    function getV2 return number;           -- public subprogram
  end;
```

```plsql
create or replace package body pack1
  is
    v2 number;                           -- private variable

    function message(a varchar2)            -- private subprogram
      return varchar2
    is
      function text                         -- local subprogram
      return varchar2
      is
      begin
        return ' 수행';
      end;
    begin
      return a||text;
    end;   

    procedure setV2(a number)               -- public subprogram
    is
      v3 number := 0;                    -- local variable
    begin
      p.p(message('setV2'));
      v2 := a + v3;
    end;

    function getV2 return number         -- public subprogram
    is
    begin
      p.p(message('getV2'));
      return v2;
    end;
  end;
```

스펙에 정의해 놓은 sub program만 body에서 public program이 된다. 그 이외에 정의한 subprogram들은 private 이다.

### 패키지의 장점 5가지(500p)

* 모듈성
  
  서브프로그램을 포함한 여러 Pl/SQL 구성 요소를 모듈화할 수 있습니다. 모듈성은 잘 묶어 둔다는 뜻으로 프로그램의 이해를 쉽게 하고 패키지 사이의 상호 작용을 더 간편하고 명료하게 해 주는 역할을 합니다.

* 쉬운 응용 프로그램 설계
  
  패키지에 포함할 서브프로그램은 완벽하게 완성되지 않아도 정의가 가능합니다. 이 때문에 전체 소스 코드를 다 작성하기 전에 미리 패키지에 저장할 서브프로그램을 지정할 수 있으므로 설계가 수월해집니다.

* 정보 은닉
  
  제작 방식에 따라 패키지에 포함하는 서브프로그램의 외부 노출 여부 또는 접근 여부를 지정할 수 있습니다. 즉 서브프로그램을 사용할 대 보안을 강화할 수 있습니다.

* 기능성 향상
  
  패키지 내부에는 서브프로그램 외에 변수 커서 예외 등도 각 세션이 유지되는 동안 선언해서 publci으로 사용할 수 있습니다. 예를 들어 특정 커서 데이터는 세션이 종료되기 전까지 보존되므로 여러 서브프로그램에서 사용할 수 있습니다.

* 성능 향상
  
  패키지를 사용할 때 피키지에 포함한 모든 서브프로그램이 메모리에 한 번에 로딩되는데 메모리에 로딩되 후의 호출은 디스크의 I/O를 일으키지 않으므로 성능이 향상됩니다.

### 트리거

데이터베이스 안의 특정 상황이나 동작, 즉 이벤트가 발생할 경우에 자동으로 실행되는 기능을 정의하는 PL/SQL 서브프로그램

* DML Trigger
  
  Insert, Update, Delete 와 같은 DML명령어를 기점으로 동작

* DDL Trigger
  
  CREATE, ALTER, DROP과 같은 DDL 명령어를 기점을 동작

* Database events Trigger
  
  데이터베이스나 스키마 이벤트를 기점으로 동작

### DML Trigger

* DML 문장 트리거
  
  ```plsql
  CREATE OR REPLACE TRIGGER secure_emp
      BEFORE INSERT or UPDATE or DELETE ON emp --요기 
      BEGIN
        IF (TO_CHAR(SYSDATE,'DY') IN ('SAT','SUN')) OR (TO_CHAR(SYSDATE,'HH24:MI') NOT BETWEEN '08:00' AND '12:00') THEN
          RAISE_APPLICATION_ERROR(-20500, 'You may insert into EMP table only during business hours.');
        END IF;
      END;
  ```

* DML 행 트리거
  
  ```plsql
  CREATE OR REPLACE  TRIGGER restrict_salary
      BEFORE INSERT OR UPDATE OF sal ON emp FOR EACH ROW --요기 
      BEGIN
        IF NOT (:NEW.job IN ('PRESIDENT', 'MANAGER')) AND :NEW.sal > 5000 THEN
          RAISE_APPLICATION_ERROR (-20202, 'Employee cannot earn more than $5,000.');
        END IF;
      END;
  ```
  
  행 트리거에는 `:NEW`와 `:OLD` 를 사용할 수 있다.

만약 트리거 안에서 에러가 나면 트리거를 발생시킨 SQL문도 취소 된다.

### DDL Trigger

```plsql
CREATE OR REPLACE TRIGGER grant_connect 
  AFTER CREATE ON DATABASE
  WHEN (dictionary_obj_type = 'USER')
  begin
    execute immediate 'grant connect,resource to '||dictionary_obj_name;
  end;
```

데이터 베이스를 새로 만들면 생성한 사용자에게 권한을 주는 트리거

```plsql
  CREATE OR REPLACE TRIGGER grant_select 
  AFTER CREATE ON schema
  WHEN (sys.dictionary_obj_type = 'TABLE')
  begin
    execute immediate 'grant select on '|| sys.dictionary_obj_name || ' to joo' ;
  end;
```

스키마를 만들면 테이블에 권한을 주는 트리거

### Database Event Triggers

```plsql
  create table log_trig_table 
  (user_id  varchar2(30),
   log_date date,
   action   varchar2(30));

  CREATE OR REPLACE TRIGGER logon_trig
  AFTER LOGON ON SCHEMA
  BEGIN
    INSERT INTO log_trig_table(user_id,log_date,action)
    VALUES (USER, SYSDATE, 'Logging on');
  END;
  /

    ------

  CREATE TABLE up_time
  (action_date DATE,
   action       VARCHAR2(20));
   
  CREATE OR REPLACE TRIGGER db_start 
  AFTER STARTUP ON DATABASE
  BEGIN
    insert into system.up_time values(sysdate,'STARTUP');
  END;
  /
```

### 트리거 구현 예제

* 만약 emplyees 테이블에 DML이 호출이 될때 날짜를 비교해서 원하는 날짜에만 DML이 수행되게 하고 싶을 때

```plsql
    CREATE OR REPLACE TRIGGER secure_emp
    BEFORE INSERT OR UPDATE OR DELETE ON employees
    DECLARE
      dummy PLS_INTEGER;
    BEGIN
      IF (TO_CHAR (SYSDATE, 'DY') IN ('SAT','SUN')) THEN
        RAISE_APPLICATION_ERROR(-20506,'You may only change data during normal business hours.');
      END IF;

      SELECT COUNT(*) INTO dummy FROM holiday
      WHERE holiday_date = TRUNC (SYSDATE);

      IF dummy > 0 THEN
        RAISE_APPLICATION_ERROR(-20507, 'You may not change data on a holiday.');
      END IF;
    END;
```

이런 복잡한 비즈니스role은 사용자 권한 부여 만으로는 할 수 없다. 물론 자바쪽에서 제어해 줄 수 있다.

* 만약 데이터 무결성을 원할때 (비즈니스 룰에 따른다.)

```plsql
    CREATE OR REPLACE TRIGGER check_salary
    BEFORE UPDATE OF salary ON employees
    FOR EACH ROW
    WHEN (NEW.salary < OLD.salary)
    BEGIN
      RAISE_APPLICATION_ERROR (-20508, 'Do not decrease salary.');
    END;
```

* 참조 무결성

```plsql
    ALTER TABLE employees
      ADD CONSTRAINT emp_deptno_fk
      FOREIGN KEY (department_id)
      REFERENCES departments(department_id)
      ON DELETE CASCADE;

      ------

    CREATE OR REPLACE TRIGGER cascade_updates
    AFTER UPDATE OF department_id ON departments
    FOR EACH ROW
    BEGIN
      UPDATE employees
      SET employees.department_id=:NEW.department_id
      WHERE employees.department_id=:OLD.department_id;

      UPDATE job_history
      SET department_id=:NEW.department_id
      WHERE department_id=:OLD.department_id;
    END;
```

* 테이블 복제

```plsql
    CREATE MATERIALIZED VIEW emp_copy
    NEXT sysdate + 7 
    AS SELECT * FROM employees@ny; -- 7일에 한번씩 반영 

      ------

    CREATE OR REPLACE TRIGGER emp_replica
    BEFORE INSERT OR UPDATE ON employees
    FOR EACH ROW
    BEGIN
      IF INSERTING THEN
        IF :NEW.flag IS NULL THEN
          INSERT INTO employees@sf 
          VALUES(:new.employee_id,...,'B');
          :NEW.flag := 'A';
        END IF;
      ELSE   /* Updating. */
        IF :NEW.flag = :OLD.flag THEN
          UPDATE employees@sf 
          SET ename=:NEW.last_name,...,flag=:NEW.flag
          WHERE employee_id = :NEW.employee_id;
        END IF;
        IF :OLD.flag = 'A' THEN
          :NEW.flag := 'B';
        ELSE
          :NEW.flag := 'A'; 
        END IF;
      END IF;
    END;
```

* 파생된 데이터 계산

```plsql
UPDATE departments d
    SET total_sal=(SELECT SUM(salary)
                   FROM employees d
                   WHERE e.department_id = d.department_id);

      ------

    CREATE PROCEDURE increment_salary
    (id NUMBER, new_sal NUMBER) 
    IS
    BEGIN
      UPDATE departments
         SET total_sal = NVL (total_sal, 0)+ new_sal
       WHERE department_id = id;
    END increment_salary;
    /
 
    CREATE OR REPLACE TRIGGER compute_salary
    AFTER INSERT OR UPDATE OF salary OR DELETE ON employees
    FOR EACH ROW
    BEGIN
      IF DELETING THEN
        increment_salary(:OLD.department_id,(-1*:OLD.salary));
      ELSIF UPDATING THEN
        increment_salary(:NEW.department_id,(:NEW.salary-:OLD.salary));
      ELSE
        increment_salary(:NEW.department_id,:NEW.salary); --INSERT
      END IF;
    END;
```

* 이벤트 로깅

```plsql
    CREATE OR REPLACE TRIGGER notify_reorder_rep
    BEFORE UPDATE OF quantity_on_hand, reorder_point 
    ON inventories FOR EACH ROW
    DECLARE
      dsc product_descriptions.product_description%TYPE;
      msg_text VARCHAR2(2000);
    BEGIN
      IF :NEW.quantity_on_hand <= :NEW.reorder_point THEN 
        SELECT product_description INTO dsc
        FROM product_descriptions 
        WHERE product_id = :NEW.product_id;
        msg_text := 'ALERT: INVENTORY LOW ORDER:'||'Yours,' ||CHR(10) ||user || '.'|| CHR(10);
      ELSIF :OLD.quantity_on_hand >= :NEW.quantity_on_hand THEN
        msg_text := 'Product #'||... CHR(10);    
      END IF;

      UTL_MAIL.SEND('inv@oracle.com','ord@oracle.com', message=>msg_text, subject=>'Inventory Notice');
    END;
    /
```

> UTL_MAIL을 사용하려면 DBMS서버에 UTL_MAIL서버 세팅을 해줘야 마.

### Instead of Trigger

view에 대해서 insert나 update를 했을 때 발생하는 트리거

View에서 기존 테이블에 없는 pk를 가진 데이터를 넣으려고 할때는 에러가 난다. **ORA-01779** 이런 경우 View에 대해 Trigger를 붙인 다음 View에 매핑되는 테이블에 데이터를 직접 넣어서 View에 데이터를 넣는 것 처럼 보이게 할 수 있다.

```plsql
create or replace trigger emp_dept_tri
    instead of insert on emp_dept
    for each row
    begin
      insert into t2(empno, ename, job, sal, deptno)
      values (:new.empno, :new.ename, :new.job, :new.sal, :new.emp_deptno); 
 
      insert into t1(deptno, dname, loc)
      values (:new.dept_deptno, :new.dname, :new.loc);  
    end;
```

위 처럼 t2 테이블과 t1테이블의 조인 결과를 담은 emp_dept 뷰 테이블에 insert를 하는 경우 트리거를 이용해서 각 테이블에 원하는 데이터를 넣는다.